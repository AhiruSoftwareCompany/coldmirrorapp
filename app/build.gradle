apply plugin: 'com.android.application'

/*
    Automatic version increment adapted from
    http://devdeeds.com/auto-increment-build-number-using-gradle-in-android/
 */

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    def versionPropsFile = file('version.properties')
    def versionBuild

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }


    ext.autoIncrementBuildNumber = {

        if (versionPropsFile.canRead()) {
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new FileNotFoundException("Could not read version.properties!")
        }
    }

    defaultConfig {
        applicationId "com.coldmirrorapp"
        minSdkVersion 21 // was 15
        targetSdkVersion 28
        versionCode versionBuild
        versionName "1.3." + versionBuild
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    flavorDimensions "version"
    productFlavors {

        fdroid {
            versionNameSuffix "-fdroid"
        }

        googleplay {
            versionNameSuffix "-googleplay"
        }
    }

    autoIncrementBuildNumber()


}
dependencies {
    implementation 'com.google.firebase:firebase-appindexing:17.1.0'
    implementation 'com.android.support:support-v4:28.+'
    /* 28.+ means any version in the 28 major release */
}
